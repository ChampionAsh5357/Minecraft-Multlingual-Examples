// Add external plugins
pluginManagement {
    // Allow for property defined plugin versions
    plugins {
        id 'fabric-loom' version fabricLoomVersion apply false
        id 'org.jetbrains.kotlin.jvm' version kotlinVersion apply false
    }

    repositories {
        gradlePluginPortal()
        maven {
            name 'Forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            name 'Fabric'
            url 'https://maven.fabricmc.net'
        }
    }

    // Resolve any plugins that don't conform with gradle format
    resolutionStrategy.eachPlugin {
        if (requested.id.toString() == 'net.minecraftforge.gradle')
            useModule group: requested.id, name: 'ForgeGradle', version: forgeGradleVersion
    }
}

// Set project data
rootProject.name = 'multilingual_examples'
include 'shared_resources'
include 'forge_java', 'forge_kotlin', 'forge_scala', 'forge_groovy'
include 'fabric_java', 'fabric_kotlin', 'fabric_scala', 'fabric_groovy'
