// Add external plugins
pluginManagement {
    // Allow for property defined plugin versions
    plugins {
        id 'fabric-loom' version fabricLoomVersion apply false
        id 'org.jetbrains.kotlin.jvm' version kotlin15Version apply false
    }

    repositories {
        gradlePluginPortal()
        maven {
            name 'Forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            name 'Fabric'
            url 'https://maven.fabricmc.net'
        }
    }

    // Resolve any plugins that don't conform with gradle format
    resolutionStrategy.eachPlugin {
        if (requested.id.toString() == 'net.minecraftforge.gradle')
            useModule group: requested.id, name: 'ForgeGradle', version: forgeGradleVersion
    }
}

// Set project data
rootProject.name = 'multilingual_examples'
include 'forge_java', 'forge_kotlin_15', 'forge_scala_2', 'forge_scala_3', 'forge_groovy_3'
include 'fabric_java', 'fabric_kotlin_15', 'fabric_scala_2', 'fabric_scala_3', 'fabric_groovy_3'
