// Add plugins
plugins {
    id 'java-conventions'
    id 'license-manager'
    id 'net.minecraftforge.gradle'
}

// Set project information
classifier.id = project.projectForgeJava
classifier.version = project.forgeJavaVersion
classifier.implFromSpec { "$project.minecraftVersion-$it" }
def modId = project.projectBaseName.replaceAll('-', '_')
def moduleId = "${modId}.${project.projectForgeJava.replaceAll('-', '_')}"

// Include resources from data generators
project.evaluationDependsOn(':shared_resources')
sourceSets {
    generated {}
    main.resources {
        srcDirs project(':shared_resources').sourceSets.forge.resources.srcDirs
        srcDirs sourceSets.generated.resources.srcDirs
    }
}

// Include MinecraftForge as a dependency
dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "$project.minecraftVersion-$project.forgeVersion"
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format:
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly
    // stable     #          Stables are built at the discretion of the MCP team
    // official   MCVersion  Official field/method names from Mojang mapping files
    //
    // You must be aware of the Mojang license when using the 'official' mappings
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Use non-defaultW mappings at your own risk. they may not always work
    // Simply re-run your setup task after changing the mappings to update your workspace
    mappings channel: project.forgeMappingChannel, version: project.minecraftVersion

    // Access Transformers can change the visibility of a particular type. Set to 'true' to enable
    if(project.hasProperty('forgeAccessTransformer') && project.getProperty('forgeAccessTransformer').toBoolean()) {
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    }

    // Default run configurations
    // These can be tweaked, removed, or duplicated as needed
    runs {
        // Client run configuration
        client {
            // Directory for the project to run in
            workingDirectory file('run/client')

            // Add IntelliJ module naming
            ideaModule "${moduleId}.main"

            // Set the console logging level
            property 'forge.logging.console.level', 'debug'

            // Attach the sources to the run
            mods.create(project.projectBaseName).source(sourceSets.main)
        }

        // Server run configuration
        server {
            workingDirectory file('run/server')
            ideaModule "${moduleId}.main"
            property 'forge.logging.console.level', 'debug'
            mods.create(project.projectBaseName).source(sourceSets.main)
        }


        // Data run configuration
        data {
            workingDirectory file('run/data')
            ideaModule "${moduleId}.main"
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources
            args '--mod', modId,
                    '--all',
                    '--output', sourceSets.generated.resources.srcDirs[0]

            sourceSets.main.resources.srcDirs.forEach {
                args '--existing', it
            }

            mods.create(project.projectBaseName).source(sourceSets.main)
        }
    }
}

jar {
    // Reobfuscate the jar file to be used in production
    finalizedBy 'reobfJar'
}
