// Add plugins
plugins {
    id 'java-conventions'
    id 'license-manager'
    alias(libs.plugins.fabric.loom)
    id 'scala'
    id 'idea'
}

// Set project information
classifier.id = project.projectFabricScala
classifier.version = project.fabricScalaVersion

// Include resources from data generators
project.evaluationDependsOn(':shared_resources')
sourceSets {
    main.resources {
        srcDirs project(':shared_resources').sourceSets.main.resources.srcDirs
        srcDirs project(':shared_resources').sourceSets.generated.resources.srcDirs
        exclude '.cache/'
    }
}

// Include Fabric as a dependency
dependencies {
    minecraft libs.minecraft
    mappings loom.officialMojangMappings()
    modImplementation libs.bundles.fabric

    // Add scala libraries (TODO: Fix sdk not being set up in intellij)
    implementation libs.scala.library
    modImplementation libs.fabric.scala
}

// Replace version in json with actual version
processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

jar {
    // Reobfuscate the jar file to be used in production
    finalizedBy 'remapJar'
}
